/*
 * Copyright (c) 2021 Ubique Innovation AG <https://www.ubique.ch>
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * SPDX-License-Identifier: MPL-2.0
 */

plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'org.sonarqube' version '3.0'
	id 'ch.ubique.gradle.ubdiag'
}

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

android {
	compileSdkVersion 30

	defaultConfig {
		applicationId "ch.admin.bag.covidcertificate.verifier"
		minSdkVersion readPropertyWithDefault('minSdkVersion', '23').toInteger()
		targetSdkVersion 30
		versionCode 2900
		versionName "2.9.0"

		// Stops the Gradle pluginâ€™s automatic rasterization of vectors
		generatedDensities = []

		buildConfigField "long", "BUILD_TIME", readPropertyWithDefault('buildTimestamp', System.currentTimeMillis()) + 'L'

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}

	productFlavors {
		// These app tokens are reserved for the official COVID Certificate Check app.
		// If you intend to integrate the CovidCertificate-SDK into your app, please get in touch with BIT/BAG to get a token assigned.
		dev {
			buildConfigField "String", "BASE_URL", '"https://www.cc-d.bit.admin.ch/app/verifier/v1/"'
			buildConfigField "String", "SDK_APP_TOKEN", '"7f64903d-4420-4cc3-ac90-c14306b5e556"'
			applicationIdSuffix '.dev'
		}
		abn {
			buildConfigField "String", "BASE_URL", '"https://www.cc-a.bit.admin.ch/app/verifier/v1/"'
			buildConfigField "String", "SDK_APP_TOKEN", '"f731fd3b-cb55-4cfd-9c46-fb3a927ffcd8"'
			applicationIdSuffix '.abn'
		}
		prod {
			buildConfigField "String", "BASE_URL", '"https://www.cc.bit.admin.ch/app/verifier/v1/"'
			buildConfigField "String", "SDK_APP_TOKEN", '"25958ed0-7790-4846-9341-7c7ef87ec389"'
		}
	}

	flavorDimensions "version"

	signingConfigs {
		release {
			storeFile file(readPropertyWithDefault('keystoreFile', 'testKeystore'))
			storePassword readProperty('keystorePassword')
			keyAlias readPropertyWithDefault('keyAlias', 'keyAlias')
			keyPassword readProperty('keyAliasPassword')
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	compileOptions {
		coreLibraryDesugaringEnabled true

		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	kotlinOptions {
		jvmTarget = '1.8'
	}

	buildFeatures {
		viewBinding true
	}
}


sonarqube {
	properties {
		property "sonar.projectName", "COVID Certificate Check App Android"
		property "sonar.projectKey", "admin-ch_CovidCertificate-App-Android"
		property "sonar.organization", "admin-ch"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

dependencies {
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation 'com.google.android.material:material:1.4.0'
	implementation project(':common')

	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

	implementation 'androidx.core:core-ktx:1.6.0'
	implementation 'androidx.appcompat:appcompat:1.3.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
	implementation 'androidx.fragment:fragment-ktx:1.3.6'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
	implementation "androidx.viewpager2:viewpager2:1.0.0"
	implementation 'androidx.security:security-crypto:1.0.0'

	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
	implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
	implementation 'com.squareup.moshi:moshi:1.12.0'
	kapt "com.squareup.moshi:moshi-kotlin-codegen:1.12.0"

	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.3'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}